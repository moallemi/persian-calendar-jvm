afterEvaluate {
    publishing {
        publications {
            "$project.name"(MavenPublication) {
                groupId = rootProject.group
                artifactId = project.artifactId
                version = rootProject.version

                if (project.plugins.hasPlugin("com.android.library")) {
                    from components.debug
                } else {
                    from components.java
                    artifact tasks["sourcesJar"]
                    artifact tasks["javadocJar"]
                }

                pom {
                    name = project.name
                    packaging "jar"
                    description = project.description
                    url = project.url
                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'moallemi'
                            name = 'Reza Moallemi'
                        }
                    }
                    scm {
                        url = project.url
                        connection = project.scm.connectionUrl
                        developerConnection = project.scm.developerConnectionUrl
                    }
                }
            }
        }
    }
}